apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    compileSdk 34

    defaultConfig {
        applicationId "payment.sdk.android.demo"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode Integer.parseInt(System.getenv("VERSION_CODE") ?: "111")
        versionName System.getenv("VERSION_NAME") ?: "4.0.7-rc2"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file("release.keystore")
            storePassword System.getenv("SIGNING_KEYSTORE_PASSWORD") ?: "Test@1234"
            keyAlias System.getenv("SIGNING_KEY_ALIAS") ?: "key0"
            keyPassword System.getenv("SIGNING_KEY_PASSWORD") ?: "Test@1234"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        demoDebug {
            dimension "version"
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            resValue "string", "app_name", "Demo App (Debug)"
        }

        demoRelease {
            dimension "version"
            signingConfig signingConfigs.release
            resValue "string", "app_name", "Demo App"
        }
    }

    buildFeatures {
        compose = true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }

    // TODO add lint rules, and fix lint errors
    lintOptions {
        quiet true
        abortOnError false
        warningsAsErrors true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.1"
    }

    namespace 'payment.sdk.android'
}

dependencies {
    implementation "androidx.core:core-ktx:1.12.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"

    implementation "androidx.activity:activity-compose:1.8.0"
    implementation platform("androidx.compose:compose-bom:2023.08.00")
    implementation "androidx.navigation:navigation-compose:2.6.0"
    implementation "androidx.compose.material3:material3:1.2.0"

    implementation 'com.google.android.gms:play-services-wallet:19.4.0'

    implementation "com.google.code.gson:gson:2.10.1"

    implementation "androidx.test:core-ktx:1.5.0"
    implementation 'androidx.appcompat:appcompat:1.7.0'
    testImplementation "junit:junit:4.13.2"
    testImplementation "androidx.arch.core:core-testing:2.2.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.2"
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"
    testImplementation "org.hamcrest:hamcrest-library:1.3"
    testImplementation "io.mockk:mockk:1.13.7"

    implementation "androidx.compose.ui:ui-tooling-preview:1.6.7"
    debugImplementation "androidx.compose.ui:ui-tooling:1.6.7"

//    implementation "com.github.network-international.payment-sdk-android:payment-sdk-core:3.1.7"
//    implementation "com.github.network-international.payment-sdk-android:payment-sdk:3.1.7"
//    implementation "com.github.network-international.payment-sdk-android:payment-sdk-samsungpay:3.1.7"

    implementation project(':payment-sdk')
    implementation project(':payment-sdk-core')
    implementation project(':payment-sdk-samsungpay')
}

repositories {
    mavenCentral()
}